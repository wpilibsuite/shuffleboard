plugins {
    id 'java-library'
}

ext.platformSpecSkipPublishing = true

description = """
The bundled CameraServer plugin. This plugin provides data sources and widgets for viewing MJPEG streams from the WPILib CameraServer.
""".trim()

def javaCppVersion = "1.5.7"


def nativeConfigName = 'wpilibNatives'
def nativeConfig = configurations.create(nativeConfigName)

def nativeTasks = wpilibTools.createExtractionTasks {
    configurationName = nativeConfigName
    resourceFileName = "ResourceInformation-CameraServer.json"
}

nativeTasks.addToSourceSetResources(sourceSets.main)

wpilibTools.deps.wpilibVersion = "2026.+"

nativeConfig.dependencies.add wpilibTools.deps.cscore()

dependencies {
    api project(":plugins:networktables")
    api wpilibTools.deps.wpilibJava("cscore")

    implementation group: "org.bytedeco", name: "javacv", version: javaCppVersion

    runtimeOnly group: "org.bytedeco", name: "ffmpeg", version: "5.0-$javaCppVersion", classifier: wpilibTools.platformMapper.javaCppClassifier
    runtimeOnly group: "org.bytedeco", name: "opencv", version: "4.5.5-$javaCppVersion", classifier: wpilibTools.platformMapper.javaCppClassifier
    runtimeOnly group: "org.bytedeco", name: "openblas", version: "0.3.19-$javaCppVersion", classifier: wpilibTools.platformMapper.javaCppClassifier
}

def testNativeConfigName = 'wpilibTestNatives'
def testNativeConfig = configurations.create(testNativeConfigName)

def folder = project.layout.buildDirectory.dir('NativeTest')

def testNativeTasks = wpilibTools.createExtractionTasks {
    taskPostfix = "Test"
    configurationName = testNativeConfigName
    rootTaskFolder.set(folder)
}

testNativeTasks.addToSourceSetResources(sourceSets.test)

testNativeConfig.dependencies.add wpilibTools.deps.cscore()
testNativeConfig.dependencies.add wpilibTools.deps.wpilib("ntcore")
testNativeConfig.dependencies.add wpilibTools.deps.wpilib("wpinet")
testNativeConfig.dependencies.add wpilibTools.deps.wpilib("wpiutil")
